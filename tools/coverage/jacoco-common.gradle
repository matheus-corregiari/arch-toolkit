apply plugin: 'jacoco'

jacoco {
    toolVersion = "$versions.jacoco"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

private static void addPathIfExists(List<String> paths,
                                    String directoryPath) {
    if (new File(directoryPath).exists())
        paths.add(directoryPath)
}

private static void addFileTreeIfExists(Project project, List<FileTree> fileTrees,
                                 String directoryPath, List<String> excludeFilter = []) {
    if (new File(directoryPath).exists())
        fileTrees.add(project.fileTree(dir: directoryPath, excludes: excludeFilter))
}

static List<String> findSourceDirectories(Project project, String flavor = "", String buildType = "debug") {

    final sourcePaths = []
    final projectDir = project.projectDir

    // Main
    addPathIfExists(sourcePaths, "$projectDir/src/main/java")
    addPathIfExists(sourcePaths, "$projectDir/src/main/kotlin")

    // Flavor
    if (!flavor.isEmpty()) {
        addPathIfExists(sourcePaths, "$projectDir/src/$flavor/java")
        addPathIfExists(sourcePaths, "$projectDir/src/$flavor/kotlin")
    }

    // BuildType
    if (!buildType.isEmpty()) {
        addPathIfExists(sourcePaths, "$projectDir/src/$buildType/java")
        addPathIfExists(sourcePaths, "$projectDir/src/$buildType/kotlin")
    }
    return sourcePaths
}

static List<ConfigurableFileTree> findClassDirectories(Project project, String flavor = "", String buildType = "debug") {

    final filter = [
            java  : ['**/R.class', '**/R$*.class', '**/BuildConfig.*'],
            kotlin: []
    ]

    final classPaths = []
    final buildDir = project.buildDir

    // BuildType
    if (!buildType.isEmpty()) {
        addFileTreeIfExists(project, classPaths, "$buildDir/intermediates/classes/$buildType", filter.java)
        addFileTreeIfExists(project, classPaths, "$buildDir/intermediates/javac/$buildType/compile${buildType.capitalize()}JavaWithJavac", filter.java)
        addFileTreeIfExists(project, classPaths, "$buildDir/tmp/kotlin-classes/$buildType", filter.kotlin)
    }

    // Flavor
    if (!flavor.isEmpty()) {
        addFileTreeIfExists(project, classPaths, "$buildDir/intermediates/classes/$flavor/$buildType", filter.java)
        addFileTreeIfExists(project, classPaths, "$buildDir/intermediates/javac/$flavor${buildType.capitalize()}/compile${flavor.capitalize()}${buildType.capitalize()}JavaWithJavac", filter.java)
        addFileTreeIfExists(project, classPaths, "$buildDir/tmp/kotlin-classes/$flavor${buildType.capitalize()}", filter.kotlin)
    }

    return classPaths
}

static List<String> findCoverageFiles(String flavor = "", String buildType = "debug") {
    if (flavor.isEmpty()) {
        return [
                "jacoco/test${buildType.capitalize()}UnitTest.exec",
                "outputs/code-coverage/connected/*coverage.ec"
        ]
    } else {
        return [
                "jacoco/test${flavor.capitalize()}${buildType.capitalize()}UnitTest.exec",
                "outputs/code-coverage/connected/flavors/${flavor.toUpperCase()}/*coverage.ec"
        ]
    }
}

static List<Project> listAllChildProjectWithoutChild(Project project) {

    final childProjects = []

    if (project.childProjects.isEmpty()) {
        childProjects.add(project)
    } else {
        project.childProjects.each {
            childPair ->
                childProjects.addAll(listAllChildProjectWithoutChild(childPair.value))
        }
    }

    return childProjects
}

ext {

    findSourceDirectories = this.&findSourceDirectories
    findClassDirectories = this.&findClassDirectories
    findCoverageFiles = this.&findCoverageFiles
    listAllChildProjectWithoutChild = this.&listAllChildProjectWithoutChild

    minCoverage = 0.70
}