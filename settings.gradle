// Setting up all repository plugins
pluginManagement {
    apply from: "$rootDir/tools/repositories.gradle"
    repositories(repositoryList)
}

dependencyResolutionManagement {
    apply from: "$rootDir/tools/repositories.gradle"
    repositoriesMode.set(RepositoriesMode.FAIL_ON_PROJECT_REPOS)
    repositories(repositoryList)
    versionCatalogs {
        libraries {
            from(files("$rootDir/tools/libs.versions.toml"))

            // Comment this when you start to update some libraries
            library("x-normalize-001", "androidx.activity:activity:1.8.0")
            library("x-normalize-002", "androidx.annotation:annotation-experimental:1.3.0")
            library("x-normalize-003", "androidx.arch.core:core-common:2.2.0")
            library("x-normalize-004", "androidx.arch.core:core-runtime:2.2.0")
            library("x-normalize-005", "androidx.collection:collection:1.1.0")
            library("x-normalize-006", "androidx.core:core-ktx:1.9.0")
            library("x-normalize-007", "androidx.customview:customview:1.1.0")
            library("x-normalize-008", "androidx.drawerlayout:drawerlayout:1.1.1")
            library("x-normalize-009", "androidx.lifecycle:lifecycle-common:2.6.2")
            library("x-normalize-010", "androidx.viewpager2:viewpager2:1.1.0-beta02")
            library("x-normalize-011", "androidx.test:monitor:1.6.1")
            library("x-normalize-012", "androidx.test:runner:1.5.2")
            library("x-normalize-013", "com.google.auto.value:auto-value-annotations:1.9")
            library("x-normalize-014", "com.google.code.findbugs:jsr305:3.0.2")
            library("x-normalize-015", "com.squareup:javawritter:2.5.0")
            library("x-normalize-016", "org.jetbrains.kotlin:kotlin-stdlib:1.9.10")
            library("x-normalize-017", "org.jetbrains.kotlin:kotlin-stdlib-common:1.9.20")
            library("x-normalize-018", "org.jetbrains.kotlin:kotlin-reflect:1.9.10")
            library("x-normalize-019", "org.jetbrains.kotlinx:kotlinx-coroutines-core-jvm:1.6.4")
            library("x-normalize-020", "org.jetbrains:annotations:20.1.0")
        }
    }
}

// Root Project config
rootProject.name = "Arch Toolkit"

// Plugins --> The Dev just want to be happy
includeBuild 'plugin'

// Toolkit Libraries
include ':toolkit:delegate'
include ':toolkit:event-observer'
include ':toolkit:foldable'
include ':toolkit:recycler-adapter'
include ':toolkit:splinter'
include ':toolkit:statemachine'

// Samples
final Boolean isIdeBuild = properties["android.injected.invoked.from.ide"] == "true"
if (isIdeBuild) {
    include ':samples:github-list-project'
//    include ':samples:foldable' FIXME: Uncomment
    include ':samples:livedata'
    include ':samples:recycler-adapter'
    include ':samples:statemachine'
}
